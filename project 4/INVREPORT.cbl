       IDENTIFICATION DIVISION.
       PROGRAM-ID. INVREPORT.
       AUTHOR. Michael Daly.
       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT  SUPPLIER-IN
                   ASSIGN TO "/home/mike/project4/SUPPLIER"
                   ORGANIZATION IS INDEXED
                   ACCESS MODE IS RANDOM
                   RECORD KEY IS SUPPLIER-NUMBER
                   FILE STATUS IS SUPPLIER-STATUS.

           SELECT  INVENT-IN
                   ASSIGN TO "/home/mike/project4/INVENT6"
                   ORGANIZATION IS INDEXED
                   ACCESS MODE IS SEQUENTIAL
                   RECORD KEY IS PART-NUMBER-IN
                   FILE STATUS IS INVENT-STATUS.

           SELECT  INVENT-REPORT
                   ASSIGN TO "/home/mike/project4/INVREPRT.TXT"
                   ORGANIZATION IS LINE SEQUENTIAL.

           SELECT  REORDER-REPORT
                   ASSIGN TO "/home/mike/project4/REORDER.TXT"
                   ORGANIZATION IS LINE SEQUENTIAL.

       DATA DIVISION.
       FILE SECTION.
       FD  SUPPLIER-IN
           DATA RECORD IS SUPPLIER-RECORD-IN.
       01  SUPPLIER-RECORD-IN.
           05  SUPPLIER-NUMBER     PIC X(5).
           05  SUPPLIER-NAME       PIC X(15).

       FD  INVENT-IN
           DATA RECORD IS INVENTORY-RECORD.
           COPY "/home/mike/project4/INVRECORD.cpy".

       FD  INVENT-REPORT
           DATA RECORD IS INVENTORY-OUT.
       01  INVENTORY-OUT               PIC X(60).

       FD  REORDER-REPORT
           DATA RECORD IS REORDER-OUT.
       01  REORDER-OUT                 PIC X(70).

       WORKING-STORAGE SECTION.

       01  INVENT-TITLE.
           05  FILLER   PIC X(15) VALUE SPACES.
           05  FILLER   PIC X(17) VALUE "INVENTORY REPORT".

       01  INVENT-HEADER.
           05  FILLER   PIC X(1) VALUE SPACES.
           05  FILLER   PIC X(8) VALUE "NUMBER".
           05  FILLER   PIC X(22) VALUE "PART NAME".
           05  FILLER   PIC X(6) VALUE "QTY".
           05  FILLER   PIC X(10) VALUE "UNIT PRICE".

       01  INVENT-OUT.
           05  FILLER           PIC X(1) VALUE SPACES.
           05  PART-NUMBER-O    PIC X(5).
           05  FILLER           PIC X(3) VALUE SPACES.
           05  PART-NAME-O      PIC X(20).
           05  FILLER           PIC X(2) VALUE SPACES.
           05  QUANTITY-O       PIC ZZ9.
           05  FILLER           PIC X(3) VALUE SPACES.
           05  UNIT-PRICE-O     PIC Z9.99.

       01  INVENT-TOTAL.
           05  FILLER              PIC X(2) VALUE SPACES.
           05  FILLER              PIC X(13) VALUE "TOTAL VALUE".
           05  TOTAL               PIC $$$$,$$9.99.

       01  INVENT-FOOTER.
           05  FILLER              PIC X(2) VALUE SPACES.
           05  FOOTER-NAME         PIC X(15).
           05  FOOTER-COUNTER      PIC ZZZ9.

       01  REORDER-TITLE.
           05  FILLER              PIC X(15) VALUE SPACES.
           05  FILLER              PIC X(20) VALUE "REORDER REPORT".
       01  REORDER-HEADER.
           05  FILLER              PIC X(1) VALUE SPACES.
           05  FILLER              PIC X(12) VALUE "PART NUMBER".
           05  FILLER              PIC X(18) VALUE "PART NAME".
           05  FILLER              PIC X(16) VALUE "RE-ORDER POINT".
           05  FILLER              PIC X(15) VALUE "SUPPLIER NAME".

       01  REORDER-RECORD.
           05  FILLER              PIC X(1) VALUE SPACES.
           05  PART-NUMBER-R       PIC X(5).
           05  FILLER              PIC X(07) VALUE SPACES.
           05  PART-NAME-R         PIC X(20).
           05  FILLER              PIC X(4) VALUE SPACES.
           05  REORDER-R           PIC ZZ9.
           05  FILLER              PIC X(8) VALUE SPACES.
           05  SUPPLIER-NAME-R     PIC X(15).

       01  OTHER-FIELDS.
           05  EOF              PIC X(1) VALUE "N".
           05  FOUND-RECORD     PIC X(1) VALUE "N".
           05  READ-COUNTER     PIC 9(3) VALUE ZEROS.
           05  WRITE-COUNTER    PIC 9(3).
           05  LINES-COUNTER    PIC 9(2).
           05  GRAND-TOTAL      PIC 9(9)V99 VALUE ZEROS.

       01  FILE-STATUS.
           05  INVENT-STATUS    PIC X(2).
           05  SUPPLIER-STATUS  PIC X(2).

       01  LINK-PARAMETERS.
           05  LS-QUANTITY         PIC 9(3).
           05  LS-UNIT-PRICE       PIC 9(2)V99.
           05  LS-VALUE            PIC 9(5)V99.

       PROCEDURE DIVISION.
       100-PRINT-REPORTS.
           PERFORM 200-INITIATE-REPORTS.
           PERFORM 200-PRINT-REPORTS UNTIL EOF = "Y".
           PERFORM 200-TERMINATE-REPORTS.

           STOP RUN.

       200-INITIATE-REPORTS.
           PERFORM 300-OPEN-FILES.
           PERFORM 300-INITIALIZE-OTHER-FIELDS.
           PERFORM 300-READ-INVENTORY.
           PERFORM 300-PRINT-TITLES.
           PERFORM 300-PRINT-HEADERS.

       200-PRINT-REPORTS.
           PERFORM 300-CALC-INVENTORY-VALUE.
           PERFORM 300-PRINT-INVENTORY-DETAIL.
           PERFORM 300-CALC-GRAND-TOTAL.
           IF QUANTITY-IN NOT GREATER THAN REORDER-IN
               PERFORM 300-INITIALIZE-SEARCH-SUPPLIER
               PERFORM 300-SEARCH-SUPPLIER
               PERFORM 300-PRINT-REORDER-RECORD.
           PERFORM 300-READ-INVENTORY.


       200-TERMINATE-REPORTS.
           PERFORM 300-PRINT-INVENT-TOTAL.
           PERFORM 300-PRINT-INVENT-FOOTER.
           PERFORM 300-CLOSE-INVENTORY-FILES.


       300-OPEN-FILES.
           OPEN INPUT INVENT-IN
                INPUT SUPPLIER-IN
                OUTPUT INVENT-REPORT
                OUTPUT REORDER-REPORT.


       300-INITIALIZE-OTHER-FIELDS.
           INITIALIZE OTHER-FIELDS.

       300-READ-INVENTORY.
           READ INVENT-IN AT END MOVE "Y" TO EOF
             NOT AT END ADD 1 TO READ-COUNTER
                        ADD 1 TO LINES-COUNTER.

       300-PRINT-TITLES.
           PERFORM 400-PRINT-INVENT-TITLE.
           PERFORM 400-PRINT-REORDER-TITLE.

       300-PRINT-HEADERS.
           PERFORM 400-PRINT-INVENT-HEADER.
           PERFORM 400-PRINT-REORDER-HEADER.

       300-CALC-INVENTORY-VALUE.
           MOVE QUANTITY-IN TO LS-QUANTITY.
           MOVE UNIT-PRICE-IN TO LS-UNIT-PRICE.
           CALL "COMPUTETOTAL" USING LINK-PARAMETERS.

       300-PRINT-INVENTORY-DETAIL.
           MOVE PART-NUMBER-IN TO PART-NUMBER-O.
           MOVE PART-NAME-IN TO PART-NAME-O.
           MOVE QUANTITY-IN TO QUANTITY-O.
           MOVE UNIT-PRICE-IN TO UNIT-PRICE-O.
           WRITE INVENTORY-OUT FROM INVENT-OUT.
           ADD 1 TO WRITE-COUNTER.

       300-CALC-GRAND-TOTAL.
           ADD LS-VALUE TO GRAND-TOTAL.

       300-PRINT-REORDER-RECORD.
           MOVE PART-NUMBER-IN TO PART-NUMBER-R.
           MOVE PART-NAME-IN TO PART-NAME-R.
           MOVE REORDER-IN TO REORDER-R.
           WRITE REORDER-OUT FROM REORDER-RECORD.

       300-CLOSE-INVENTORY-FILES.
           CLOSE INVENT-IN
                 SUPPLIER-IN
                 INVENT-REPORT
                 REORDER-REPORT.

       300-INITIALIZE-SEARCH-SUPPLIER.
           MOVE SPACES TO SUPPLIER-NAME-R.
           MOVE "N" TO FOUND-RECORD.

       300-SEARCH-SUPPLIER.
           MOVE SUPPLIER-NUMBER-IN TO SUPPLIER-NUMBER.
           READ SUPPLIER-IN
                   INVALID KEY MOVE SPACES TO SUPPLIER-NAME-R
                   NOT INVALID KEY MOVE SUPPLIER-NAME TO SUPPLIER-NAME-R.

       300-PRINT-INVENT-TOTAL.
           MOVE GRAND-TOTAL TO TOTAL.
           WRITE INVENTORY-OUT FROM INVENT-TOTAL
            AFTER ADVANCING 4 LINES.

       300-PRINT-INVENT-FOOTER.
           MOVE "RECORDS READ" TO FOOTER-NAME.
           MOVE READ-COUNTER TO FOOTER-COUNTER.
           WRITE INVENTORY-OUT FROM INVENT-FOOTER
            AFTER ADVANCING 2 LINES.
           MOVE "RECORDS WRITTEN" TO FOOTER-NAME.
           MOVE WRITE-COUNTER TO FOOTER-COUNTER.
           WRITE INVENTORY-OUT FROM INVENT-FOOTER.

       400-PRINT-INVENT-TITLE.
           WRITE INVENTORY-OUT FROM INVENT-TITLE
            AFTER ADVANCING 1 LINES.

       400-PRINT-REORDER-TITLE.
           WRITE REORDER-OUT FROM REORDER-TITLE
            AFTER ADVANCING 1 LINES.

       400-PRINT-INVENT-HEADER.
           WRITE INVENTORY-OUT FROM INVENT-HEADER
            AFTER ADVANCING 2 LINES.
           MOVE SPACES TO INVENTORY-OUT.
           WRITE INVENTORY-OUT.

       400-PRINT-REORDER-HEADER.
           WRITE REORDER-OUT FROM REORDER-HEADER
            AFTER ADVANCING 2 LINES.
           MOVE SPACES TO REORDER-OUT.
           WRITE REORDER-OUT.
